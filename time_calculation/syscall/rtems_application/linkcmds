ENTRY(_start)

/* Set default value */
rom_space_size    = DEFINED(rom_space_size) ? rom_space_size : 2048K;
ram_space_size    = DEFINED(ram_space_size) ? ram_space_size : 4096K;
stack_space_size  = DEFINED(stack_space_size) ? stack_space_size : 10M;
heap_space_size   = DEFINED(heap_space_size) ? heap_space_size : 50M;
user_stack_size   = DEFINED(user_stack_size) ? user_stack_size : 2M;
kernel_stack_size = DEFINED(kernel_stack_size) ? kernel_stack_size : 16K;
syscache_size     = DEFINED(syscache_size) ? syscache_size : 1M;
main_priority     = DEFINED(main_priority) ? main_priority : 150;

/* arm          0x0000000010000000 */
/* aarch64      0xFFFF000000000000 */
/* loongarch64  0x0000000010000000 */

/* ROM     (rx)     : ORIGIN = 0x0000000010000000,             LENGTH = 2048K */
/* ROM     (rx)     : ORIGIN = 0xFFFF000000000000,             LENGTH = 2048K */

MEMORY
{
    ROM     (rx)     : ORIGIN = 0x0000000010000000,             LENGTH = 2048K   
    RAM     (rwx)    : ORIGIN = ORIGIN(ROM) + LENGTH(ROM),      LENGTH = 4096K
    STACK   (rw)     : ORIGIN = ORIGIN(RAM) + LENGTH(RAM),      LENGTH = 10M
    HEAP    (rw)     : ORIGIN = ORIGIN(STACK) + LENGTH(STACK),  LENGTH = 50M
}

SECTIONS {
    . = ORIGIN(ROM);

    /* ========== code segment ========== */
    .text : ALIGN(0x4) {
        __text_start__ = .;
        *(.text.unlikely .text.*_unlikely .text.unlikely.*)
        *(.text.exit .text.exit.*)
        *(.text.startup .text.startup.*)
        *(.text.hot .text.hot.*)
        *(.text .stub .text.* .gnu.linkonce.t.*)
        *(.gnu.warning)
        __text_end__ = .;
    } > ROM
    __text_size = __text_end__ - __text_start__;

    /* ========== PLT segment ========== */
    .plt : ALIGN(0x4) {
        *(.plt)
        *(.plt.*)
        *(.iplt)
    } > ROM

    /* ========== init ========== */
    .init : ALIGN(0x4) {
        KEEP (*(SORT_NONE(.init)))
    } > ROM

    .fini : ALIGN(0x4) {
        KEEP (*(SORT_NONE(.fini)))
    } > ROM

    /* ========== C++ contructor ========== */
    .preinit_array : ALIGN(0x8) {
        PROVIDE_HIDDEN (__preinit_array_start = .);
        KEEP (*(.preinit_array))
        PROVIDE_HIDDEN (__preinit_array_end = .);
    } > ROM

    .init_array : ALIGN(0x8) {
        PROVIDE_HIDDEN (__init_array_start = .);
        KEEP (*(SORT_BY_INIT_PRIORITY(.init_array.*) SORT_BY_INIT_PRIORITY(.ctors.*)))
        KEEP (*(.init_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .ctors))
        PROVIDE_HIDDEN (__init_array_end = .);
    } > ROM

    .fini_array : ALIGN(0x8) {
        PROVIDE_HIDDEN (__fini_array_start = .);
        KEEP (*(SORT_BY_INIT_PRIORITY(.fini_array.*) SORT_BY_INIT_PRIORITY(.dtors.*)))
        KEEP (*(.fini_array EXCLUDE_FILE (*crtbegin.o *crtbegin?.o *crtend.o *crtend?.o ) .dtors))
        PROVIDE_HIDDEN (__fini_array_end = .);
    } > ROM

    /* ========== error ========== */
    .eh_frame_hdr : ALIGN(0x4) {
        KEEP (*(.eh_frame_hdr))
        *(.eh_frame_entry .eh_frame_entry.*)
    } > ROM

    .eh_frame : ALIGN(0x8) {
        PROVIDE (__eh_frame_start = .);
        KEEP (*(.eh_frame))
        KEEP (*(.eh_frame.*))
        LONG (0);
    } > ROM

    .gcc_except_table : ALIGN(0x4) {
        KEEP (*(.gcc_except_table .gcc_except_table.*))
    } > ROM

    /* ========== read only ========== */
    .rodata : ALIGN(0x4) {
        __rodata_start__ = .;
        *(.rodata .rodata.* .gnu.linkonce.r.*)
        *(.rodata1)
        __rodata_end__ = .;
    } > ROM
    __rodata_size = __rodata_end__ - __rodata_start__;

    /* ========== dynamic link ========== */
    .interp : ALIGN(0x1) {
        *(.interp)
    } > ROM

    .gnu.version : ALIGN(0x2) {
        *(.gnu.version)
    } > ROM

    .gnu.version_r : ALIGN(0x8) {
        *(.gnu.version_r)
    } > ROM

    .dynsym : ALIGN(0x8) {
        *(.dynsym)
    } > ROM

    .dynstr : ALIGN(0x1) {
        *(.dynstr)
    } > ROM

    .hash : ALIGN(0x8) {
        *(.hash)
    } > ROM

    .gnu.hash : ALIGN(0x8) {
        *(.gnu.hash)
    } > ROM

    /* ========== rotate ========== */
    .rela.dyn : ALIGN(0x8) {
        *(.rela.init)
        *(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
        *(.rela.fini)
        *(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
        *(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
        *(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
        *(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
        *(.rela.ctors)
        *(.rela.dtors)
        *(.rela.got)
        *(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
        *(.rela.ldata .rela.ldata.* .rela.gnu.linkonce.l.*)
        *(.rela.lbss .rela.lbss.* .rela.gnu.linkonce.lb.*)
        *(.rela.lrodata .rela.lrodata.* .rela.gnu.linkonce.lr.*)
        *(.rela.ifunc)
    } > ROM

    .rela.plt : ALIGN(0x8) {
        *(.rela.plt)
        *(.rela.iplt)
    } > ROM

    /* ========== self-configuration ========== */
    .shcfg : ALIGN(0x8) {
        *(.shcfg)
    } > ROM

    .ucfg : ALIGN(0x8) {
        *(.ucfg)
    } > ROM

    /* ========== data ========== */
    .data : ALIGN(0x4) {
        __data_start__ = .;
        *(.data .data.* .gnu.linkonce.d.*)
        *(.data1)
        SORT(CONSTRUCTORS)
        __data_end__ = .;
    } > RAM AT> ROM
    __data_size = __data_end__ - __data_start__;
    __data_load_start__ = LOADADDR(.data);

    /* ========== dynamic ========== */
    .dynamic : ALIGN(0x8) {
        *(.dynamic)
    } > RAM AT> ROM

    .got : ALIGN(0x8) {
        *(.got.plt) 
        *(.igot.plt) 
        *(.got)
        *(.igot)
    } > RAM AT> ROM

    .got.plt : ALIGN(0x8) {
        *(.got.plt)
        *(.igot.plt)
    } > RAM AT> ROM

    /* ========== BSS segment ========== */
    .bss (NOLOAD) : ALIGN(0x4) {
        __bss_start__ = .;
        *(.dynbss)
        *(.bss .bss.* .gnu.linkonce.b.*)
        *(COMMON)
        __bss_end__ = .;
    } > RAM
    __bss_size = __bss_end__ - __bss_start__;

    /* ========== stack ========== */
    .stack (NOLOAD) : ALIGN(0x4) {
        __stack_start__ = ORIGIN(STACK);
        . += LENGTH(STACK);
        __stack_end__ = ORIGIN(STACK) + LENGTH(STACK);
    } > STACK
    __stack_size = __stack_end__ - __stack_start__;

    /* ========== heap ========== */
    .heap (NOLOAD) : ALIGN(0x4) {
        __heap_start__ = ORIGIN(HEAP);
        . += LENGTH(HEAP);
        __heap_end__ = ORIGIN(HEAP) + LENGTH(HEAP);
    } > HEAP
    __heap_size = __heap_end__ - __heap_start__;

    /* ========== debug ========== */
    .comment        0 : { *(.comment) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_line     0 : { *(.debug_line .debug_line.* .debug_line_end) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }
    .debug_macro    0 : { *(.debug_macro) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_pubtypes 0 : { *(.debug_pubtypes) }
    .debug_ranges   0 : { *(.debug_ranges) }
    .debug_str      0 : { *(.debug_str) }
    .debug_types    0 : { *(.debug_types) }

    /* ========== discard ========== */
    /DISCARD/ : {
        *(.note.GNU-stack)
        *(.gnu_debuglink)
        *(.gnu.lto_*)
    }
}

__stack_top = ORIGIN(STACK) + LENGTH(STACK);