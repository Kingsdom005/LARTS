ENTRY(_start)

/* Set default value */
rom_space_size    = DEFINED(rom_space_size) ? rom_space_size : 2048K;
ram_space_size    = DEFINED(ram_space_size) ? ram_space_size : 4096K;
stack_space_size  = DEFINED(stack_space_size) ? stack_space_size : 10M;
heap_space_size   = DEFINED(heap_space_size) ? heap_space_size : 50M;
user_stack_size   = DEFINED(user_stack_size) ? user_stack_size : 2M;
kernel_stack_size = DEFINED(kernel_stack_size) ? kernel_stack_size : 16K;
syscache_size     = DEFINED(syscache_size) ? syscache_size : 1M;
main_priority     = DEFINED(main_priority) ? main_priority : 150;

/* arm          0x0000000010000000 */
/* aarch64      0xFFFF000000000000 */
/* loongarch64  0x0000000010000000 */

/* ROM     (rx)     : ORIGIN = 0x0000000010000000,             LENGTH = 2048K */
/* ROM     (rx)     : ORIGIN = 0xFFFF000000000000,             LENGTH = 2048K */

MEMORY
{
    ROM     (rx)     : ORIGIN = 0xFFFF000000000000,             LENGTH = 2048K   
    RAM     (rwx)    : ORIGIN = ORIGIN(ROM) + LENGTH(ROM),      LENGTH = 4096K
    STACK   (rw)     : ORIGIN = ORIGIN(RAM) + LENGTH(RAM),      LENGTH = 10M
    HEAP    (rw)     : ORIGIN = ORIGIN(STACK) + LENGTH(STACK),  LENGTH = 50M
}

SECTIONS {
    . = ORIGIN(ROM);

    .text : ALIGN(0x4) {
        __text_start__ = .;
        *(.text .text.*)
        __text_end__ = .;
    } > ROM
    __text_size = __text_end__ - __text_start__;

    .rodata : ALIGN(0x4) {
        __rodata_start__ = .;
        *(.rodata .rodata.*)
        __rodata_end__ = .;
    } > ROM
    __rodata_size = __rodata_end__ - __rodata_start__;

    .data : ALIGN(0x4) {
        __data_start__ = .;
        *(.data .data.*)
        __data_end__ = .;
    } > RAM AT> ROM 
    __data_size = __data_end__ - __data_start__;

    .bss (NOLOAD) : ALIGN(0x4) {
        __bss_start__ = .;
        *(.bss .bss.*)
        *(COMMON)
        __bss_end__ = .;
    } > RAM
    __bss_size = __bss_end__ - __bss_start__;

    .stack : ALIGN(0x4) {
        __stack_start__ = ORIGIN(STACK);
        . += LENGTH(STACK);
        __stack_end__ = ORIGIN(STACK) + LENGTH(STACK);
    } > STACK
    __stack_size = __stack_end__ - __stack_start__;

    .heap : ALIGN(0x4) {
        __heap_start__ = ORIGIN(HEAP);
        . += LENGTH(HEAP);
        __heap_end__ = ORIGIN(HEAP) + LENGTH(HEAP);
    } > HEAP
    __heap_size = __heap_end__ - __heap_start__;

}

__stack_top = ORIGIN(STACK) + LENGTH(STACK);
